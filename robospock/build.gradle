apply {
    plugin 'groovy'
    plugin 'signing'
    plugin 'maven'
    plugin 'maven-publish'
}

def artifactId = 'robospock'
group = 'org.robospock'
description = 'RoboSpock testing library'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
    def androidHome = System.getenv("ANDROID_HOME")
    maven {
        url "$androidHome/extras/android/m2repository/"
    }
}

dependencies {
    groovy "org.codehaus.groovy:groovy-all:1.8.6"
    compile "org.spockframework:spock-core:0.7-groovy-1.8"

    // Android part
    //compile "org.roboguice:roboguice:2.0"
    compile "org.robolectric:robolectric:2.3"
    compile "com.google.android:android:4.1.1.4"

    testCompile project(':antApp')
}

test {
    workingDir project(':antApp').file('.')
}

clean {
    delete 'tmp'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

/*
 *
 * It's all about OSS Sonatype from now to the bottom
 *
 */

// Set up for Sonatype OSS repository
def isReleaseVersion = !version.endsWith('SNAPSHOT')

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar the artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask(':uploadArchives') }

    sign configurations.archives
}

ext {
    repoUsername = project.hasProperty('repoUsername') ? project['repoUsername'] : ''
    repoPassword = project.hasProperty('repoPassword') ? project['repoPassword'] : ''
    releaseRepository = project.hasProperty('releaseRepo') ? project['releaseRepo'] : ''
    snapshotRepository = project.hasProperty('snapshotRepo') ? project['snapshotRepo'] : ''
}


publishing {
    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }
            url releaseRepository
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

}
